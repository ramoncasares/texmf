% FIRMA.MF (RMCG19940424)

def firma(expr charcode) =
 % typically enclosed on (4cm,2cm,2mm)
 % parameters
 save val, i, j, k, par, alfa, beta, gamma,
  exceso, grosor, base, altura, pos_acento, long_acento;
 val0uno := 60;   val0cero := 45;    % alfa
 val1uno := 60;   val1cero := 45;    % beta
 val2uno := 1;    val2cero := 5;     % exceso
 val3uno := 1pt;  val3cero := 3pt;   % grosor
 val4uno := 3/4;  val4cero := 1;     % base
 val5uno := 3/4;  val5cero := 1;     % altura
 val6uno := 0.3;  val6cero := 0.7;   % pos_acento
 val7uno := 1;    val7cero := 2;     % long_acento
 % choose values from charcode (0 <= charcode <= 255)
 i := charcode;
 for k := 0 upto 7:
  j := i mod 2; i := i div 2;
  par[k] := if odd j: val[k]uno else: val[k]cero fi;
 endfor;
 alfa := par0;       beta := par1;
 exceso := par2 * d; grosor := par3;
 base := par4 * w;   altura := par5 * h;
 pos_acento := par6; long_acento := par7 * d;
 % draw signature
 y2 = y4 = y6 = 0; x2 = 0; x4 = base/2; x6 = w;
 z2 - z1 = (exceso,0) rotated alfa;
 gamma := 1/2 * (alfa + beta);
 z3 = 1/2*(z4-z2) + (altura,0) rotated gamma;
 z51 - z4 = (d,0) rotated (180+beta);
 z52 - z4 = (d,0);
 z7 = (base/2 + pos_acento * (w - base/2), h/10);
 z8 = z7 + (long_acento,0) rotated gamma;
 pickup pencircle xscaled grosor rotated 30;
 draw z1..z2{dir alfa}..z3{right}..z4{dir(180+beta)}
  ..z51{dir(180+beta)}; draw z51..z52..z6{right};
 draw z7{dir alfa}..z8{dir beta};
 % labels(1,2,3,4,51,52,6,7,8);
enddef;

endinput;

def make_font =
 mode := localfont;
 mode_setup;
 for charcode := 0 upto 255:
  beginchar(charcode, 4cm#, 2cm#, 2mm#);
   firma(charcode); 
  endchar;
 endfor;
 font_size:=2cm#;
 font_coding_scheme:="GRAPHIC";
 font_identifier:="Firma";
enddef;

end.
